## B.4 Registers and Wires

```
Register and Wire Interfaces and Modules
Name Section Description
Reg B.4.1 Register interface
CReg B.4.2 Implementation of a register with an array ofReginterfaces that
sequence concurrently
RWire B.4.3 Similar to theReginterface with output wrapped in aMaybetype
to indicate validity
Wire B.4.4 Interchangeable with aReginterface, validity of the data is implicit
BypassWire B.4.5 Implementation of theWireinterface where the_write methodis
always_enabled
DWire B.4.6 Implementation of theWireinterface where the_readmethod is
always_readyby providing a default value
PulseWire B.4.7 Similar to theRWireinterface without any data
ReadOnly B.4.8 Interface which provides a value
WriteOnly B.4.9 Interface which writes a value
```
### B.4.1 Reg

The most elementary module available in BSV is the register, which has aReginterface. Registers
are polymorphic, i.e., in principle they can hold a value of any type but, of course, ultimately registers
store bits. Thus, the provisos on register modules indicate that the type of the value stored in the
register must be in theBitstype class, i.e., the operationspackandunpackare defined on the type
to convert into bits and back.

Note that all Bluespec registers are considered atomic units, which means that even if one bit is
updated (written), then all the bits are considered updated. This prevents multiple rules from
updating register fields in an inconsistent manner.


Reference Guide Bluespec SystemVerilog

When scheduling register modules, reads occur before writes. That is, any rule which reads from a
register must be scheduled earlier than any other rule which writes to the register. The value read
from the register is the value written in the previous clock cycle.

Interfaces and Methods

TheReginterface contains two methods,_writeand_read.

```
interface Reg #(type a_type);
method Action _write(a_type x1);
method a_type _read();
endinterface: Reg
```
The_writeand_readmethods are rarely used. Instead, for writes, one uses the non-blocking
assignment notation and, for reads, one just mentions the register interface in an expression.

```
RegInterface
Method Arguments
Name Type Description Name Description
_write Action writes a valuex1 x1 data to be written
_read a_type returns the value of the
register
```
Modules

Prelude provides three modules to create a register:mkRegcreates a register with a given reset value,
mkRegUcreates a register without any reset, andmkRegAcreates a register with a given reset value
and with asynchronous reset logic.

```
mkReg Make a register with a given reset value. Reset logic is synchronous.
```
```
module mkReg#(parameter a_type resetval)(Reg#(a_type))
provisos (Bits#(a_type, sizea));
```
```
mkRegU Make a register without any reset; initial simulation value is alternating
01 bits.
```
```
module mkRegU(Reg#(a_type))
provisos (Bits#(a_type, sizea));
```
```
mkRegA Make a register with a given reset value. Reset logic is asynchronous.
module mkRegA#(parameter a_type resetval)(Reg#(a_type))
provisos (Bits#(a_type, sizea));
```
Scheduling

When scheduling register modules, reads occur before writes. That is, any rule which reads from
a register must be scheduled earlier than any other rule which writes to the register. The value
read from the register is the value written in the previous clock cycle. Multiple rules can write to a
register in a given clock cycle, with the effect that a later rule overwrites the value written by earlier
rules.


Bluespec SystemVerilog Reference Guide

```
Scheduling Annotations
mkReg, mkRegU, mkRegA
read write
read CF SB
write SA SBR
```
Functions

Three functions are provided for using registers:asRegreturns the register interface instead of the
value of the register;readRegreads the value of a register, useful when managing vectors or lists of
registers; andwriteRegto write a value into a register, also useful when managing vectors or lists
of registers.

```
asReg Treat a register as a register, i.e., suppress the normal behavior where the
interface name implicitly represents the value that the register contains
(the_readvalue). This function returns the register interface, not the
value of the register.
```
```
function Reg#(a_type) asReg(Reg#(a_type) regIfc);
```
```
readReg Read the value out of a register. Useful for giving as the argument to
higher-order vector and list functions.
```
```
function a_type readReg(Reg#(a_type) regIfc);
```
```
writeReg Write a value into a register. Useful for giving as the argument to higher-
order vector and list functions.
function Action writeReg(Reg#(a_atype) regIfc, a_type din);
```
Examples

```
Reg#(ta) res <- mkReg(0);
```
```
// create board[x][y]
Reg#(ta) pipe[depth];
for (Integer i=0; i<depth; i=i+1) begin
Reg#(ta) c();
mkReg#(0) xinst(c);
pipe[i] = asReg(c);
end
```
```
function a readReg(Reg#(a) r);
return(r);
endfunction
```
### B.4.2 CReg

The basic register modules described in B.4.1 have scheduling annotations that do not allow two rules
to read and write a register concurrently (that is, sequentially in the same clock cycle). Implementing


Reference Guide Bluespec SystemVerilog

this concurrency requires bypassing, so that a value written by one rule is visible to the next rule
that wants to read the register. This can create long paths, and so explicit input from the designer is
preferred. Therefore, the basic registers do not support this bypassing. If a designer wants concurrent
register access between rules, they must explicitly request and manage this, by instantiating one of
theCRegfamily of modules.

These concurrent registers are also known as EHRs (Ephemeral History Registers) in work by Arvind
and Rosenband [Ros04].

Modules

Prelude provides three modules to create a concurrent register:mkCRegcreates a concurrent register
with a given reset value,mkCRegUcreates a concurrent register without any reset, andmkCRegA
creates a concurrent register with a given reset value and with asynchronous reset logic.

```
mkCReg Make a concurrent register with a given number of ports and with a given
reset value. Reset logic is synchronous.
```
```
module mkCReg#(parameter Integer n,
parameter a_type resetval)
(Reg#(a_type) ifc[])
provisos (Bits#(a_type, sizea));
```
```
mkCRegU Make a concurrent register with a given number of ports and without any
reset. Initial simulation value is alternating 01 bits.
```
```
module mkCRegU#(parameter Integer n) (Reg#(a_type) ifc[])
provisos (Bits#(a_type, sizea));
```
```
mkCRegA Make a concurrent register with a given number of ports and with a given
reset value. Reset logic is asynchronous.
```
```
module mkCRegA#(parameter Integer n,
parameter a_type resetval)
(Reg#(a_type) ifc[])
provisos (Bits#(a_type, sizea));
```
As indicated by the bracket notation in the interface type, these modules provide an array ofReg
interfaces, whose methods can be called concurrently in separate actions. The modules take a size
parametern, that indicates the size of the array to return. The minimum size is 0. An implementation
may specify a maximum size (5, in the current implementation).

Scheduling

When a concurrent register is instantiated, it returns an array ofReginterfaces. The scheduling
relationships for_readand_writein oneReginterface are the same as for the basic register modules
in B.4.1. The methods of lower-numbered interfaces sequence before the methods of higher-numbered
interfaces.

The designer manages the concurrency of the register accesses by choosing which interfaces to assign
to which rules.


Bluespec SystemVerilog Reference Guide

```
Scheduling Annotations
mkCReg, mkCRegU, mkCRegA
forj < k
readj writej readk writek
readj CF SB SBR SBR
writej SA SBR SBR SBR
```
TheReginterfaces execute in sequence starting with element 0 of the array up through elementn−1.
That is, the_readmethod of the first interface will return the value stored in the register from the
previous clock cycle; if the_writemethod of the first interface is called, the_readmethod of the
second interface will return the written value, otherwise it returns the registered value. And so on,
with each_readmethod returning the last value written to any of the lower-numbered interfaces, or
the register value if none of the lower-numbered_writemethods were called. The value registered
at the end of clock cycle is the value written by the highest-numbered write method that was called.

Examples

In the following example, the two rules can be scheduled concurrently in the same clock cycle, which
would not have been possible if the registerbyteCounthad been instantiated as a basicmkReg
register. Further, if both rules execute in a given clock cycle, the value read in ruledoRecvis the
updated value that was written in ruledoSend.

```
Reg#(Bool) byteCount[2] <- mkCReg(2, True);
```
```
rule doSend (canSend);
...
byteCount[0] <= byteCount[0] + len;
endrule
```
```
rule doRecv (canRecv);
...
byteCount[1] <= byteCount[1] - len;
endrule
```
In the above example,mkCRegreturns an array ofReginterfaces. This type is expressed implicitly
using the bracket syntax. The type can also be explicitly expressed with the nameArray, when
necessary. (See Section B.2.13 for more information on theArraytype.) One place where this can
be necessary is when the array type is a component of a larger type. A common example of this
would be when instatiating aVectorofmkCRegmodules:

```
Vector#(N, Array#(Reg#(T))) regs <- replicateM(mkCReg(3,0));
```
The above instantiation can also be achieved using multidimensional arrays. Instead of a vector of
arrays, one can use an array of arrays:

```
Integer n = valueOf(N);
Reg#(T) regs[n][3];
for (Integer i=0; i<n; i=i+1)
regs[i] <- mkCReg(3,0);
```

Reference Guide Bluespec SystemVerilog

### B.4.3 RWire

AnRWireis a primitive stateless module whose purpose is to allow data transfer between methods
and rules without the cycle latency of a register. That is, aRWiremay be written in a cycle and
that value can be read out in the same cycle; values are not stored across clock cycles.

When scheduling wire modules, since the value is read in the same cycle in which it is written, writes
must occur before reads. That is, any rule which writes to a wire must be scheduled earlier than
any other rule which reads from the wire. This is the reverse of how registers are scheduled.

Interfaces and Methods

TheRWireinterface is conceptually similar to a register’s interface, but the output value is wrapped
in aMaybetype. Thewsetmethod places a value on the wire and sets the valid signal. The read-like
method,wget, returns the value and a valid signal in aMaybetype. The output is onlyValidif a
write has a occurred in the same clock cycle, otherwise the output isInvalid.

```
RWireInterface
Method Arguments
Name Type Description Name Description
wset Action writes a value and sets the valid
signal
```
```
datain data to be sent
on the wire
wget Maybe returns the value and the valid
signal
```
```
interface RWire#(type element_type) ;
method Action wset(element_type datain) ;
method Maybe#(element_type) wget() ;
endinterface: RWire
```
Modules

ThemkRWireSBR,mkRWire, andmkUnSafeRWiremodules are provided to create an RWire. The
difference between theRWiremodules is the scheduling annotations. InmkRWireSBRthewsetis
SBR with itself, allowing multiplewsets in the same clock cycle (though not, of course, in the same
rule).

```
mkRWireSBR Creates anRWire. Output is only valid if a write has occurred in
the same clock cycle. This is the recommended module to use to
create anRWire.
module mkRWireSBR(RWire#(element_type))
provisos (Bits#(element_type, element_width)) ;
```
```
mkRWire Creates anRWire. Output is only valid if a write has occurred in
the same clock cycle. The write (wset) must be sequenced before
the read (wget) and they must be in different rules.
```
```
module mkRWire(RWire#(element_type))
provisos (Bits#(element_type, element_width)) ;
```

Bluespec SystemVerilog Reference Guide

```
mkUnsafeRWire Creates anRWire. Output is only valid if a write has occurred in
the same clock cycle. The write (wset) must be sequenced before
the read (wget) but they can be in the same rule.
```
```
module mkUnsafeRWire(RWire#(element_type))
provisos (Bits#(element_type, element_width)) ;
```
Scheduling

When scheduling wire modules, since the value is read in the same cycle in which it is written, writes
must occur before reads. That is, any rule which writes to a wire must be scheduled earlier than
any other rule which reads from the wire. This is the reverse of how registers are scheduled.

```
Scheduling Annotations
mkRWire
wget wset
wget CF SAR
wset SBR C
```
```
Scheduling Annotations
mkRWireSBR
wget wset
wget CF SAR
wset SBR SBR
```
```
Scheduling Annotations
mkUnsafeRWire
wget wset
wget CF SA
wset SB C
```
Examples

```
RWire#(int) rw_incr <- mkRWire();
```
```
rule doit;
Maybe#(int) mbi = rw_incr.wget();
int di = fromMaybe (?, mbi);
if ((! isValid (mbi))
noAction;
else // ( isValid (mbi))
value2 <= value2 + di ;
endrule
```
```
rule doitdifferently;
case (rw_incr.wget()) matches
{ tagged Invalid } : noAction;
{ tagged Valid .di} : value <= value + di;
endcase
endrule
```
```
method Action increment (int di);
rw_incr.wset (di);
endmethod
```
### B.4.4 Wire

TheWireinterface and module are similar toRWire, but the valid bit is hidden from the user and
the validity of the read is considered an implicit condition. TheWireinterface works like theReg
interface, so mentioning the name of the wire gets (reads) its contents whenever they’re valid, and
using<=writes the wire. Wireis anRWirethat is designed to be interchangeable withReg. You
can replace aRegwith aWirewithout changing the syntax.

Interfaces and Methods


Reference Guide Bluespec SystemVerilog

```
typedef Reg#(element_type) Wire#(type element_type);
```
```
WireInterface
Method Arguments
Name Type Description Name Description
_write Action writes a valuex1 x1 data to be written
_read a_type returns the value of the
wire
```
Modules

ThemkWireandmkUnsafeWiremodules are provided to create aWire. The only difference between
the two modules are the scheduling annotations. ThemkWireversion requires that the the write and
the read be in different rules.

```
mkWire Creates aWire. Validity of the output is automatically checked as
an implicit condition of the read method. The write and the read
methods must be in different rules.
```
```
module mkWire(Wire#(element_type))
provisos (Bits#(element_type, element_width));
```
```
mkUnsafeWire Creates aWire. Validity of the output is automatically checked as
an implicit condition of the read method. The write and the read
methods can be in the same rule.
module mkUnsafeWire(Wire#(element_type))
provisos (Bits#(element_type, element_width));
```
```
Scheduling Annotations
mkWire
read write
read CF SAR
write SBR C
```
```
Scheduling Annotations
mkUnsafeWire
read write
read CF SA
write SB C
```
Examples

module mkCounter_v2 (Counter);
Reg#(int) value2 <- mkReg(0);
Wire#(int) w_incr <- mkWire();

```
rule r_incr;
value2 <= value2 + w_incr;
endrule
```
```
method int read();
return value2;
endmethod
```
method Action increment (int di);
w_incr <= di;
endmethod
endmodule


Bluespec SystemVerilog Reference Guide

### B.4.5 BypassWire

BypassWire is an implementation of the Wire interface where the_writemethod is analways_enabled
method. The compiler will issue a warning if the method does not appear to be called every clock
cycle. The advantage of this tradeoff is that the_readmethod of this interface does not carry
any implicit condition (so it can satisfy ano_implicit_conditionsassertion or analways_ready
method).

```
mkBypassWire Creates aBypassWire. The write method is alwaysenabled.
```
```
module mkBypassWire(Wire#(element_type))
provisos (Bits#(element_type, element_width));
```
```
Scheduling Annotations
mkBypassWire
read write
read CF SAR
write SBR C
```
Examples

module mkCounter_v2 (Counter);
Reg#(int) value2 <- mkReg(0);
Wire#(int) w_incr <- mkBypassWire();

```
rule r_incr;
value2 <= value2 + w_incr;
endrule
```
```
method int read();
return value2;
endmethod
```
method Action increment (int di);
w_incr <= di;
endmethod
endmodule

### B.4.6 DWire

DWire is an implementation of the Wire interface where the_readmethod is analways_ready
method and thus has no implicit conditions. Unlike the BypassWire however, the_write method
need not be always enabled. On cycles when a DWire is written to, the_readmethod returns that
value. On cycles when no value is written, the_readmethod instead returns a default value that is
specified as an argument during instantiation.

There are two modules to create aDWire; the only difference being the scheduling annotations. A
write is always scheduled before a read, however themkDWiremodule requires that the write and
read be in different rules.

```
mkDWire Creates aDWire. The read method is alwaysready.
```
```
module mkDWire#(a_type defaultval)(Wire#(element_type))
provisos (Bits#(element_type, element_width));
```

Reference Guide Bluespec SystemVerilog

```
mkUnsafeDWire Creates aDWire. The read method is alwaysready.
```
```
module mkUnsafeDWire#(a_type defaultval)(Wire#(element_type))
provisos (Bits#(element_type, element_width));
```
```
Scheduling Annotations
mkDWire
read write
read CF SAR
write SBR C
```
```
Scheduling Annotations
mkUnsafeDWire
read write
read CF SA
write SB C
```
Examples

module mkCounter_v2 (Counter);
Reg#(int) value2 <- mkReg(0);
Wire#(int) w_incr <- mkDWire (0);

```
rule r_incr;
value2 <= value2 + w_incr;
endrule
```
```
method int read();
return value2;
endmethod
```
method Action increment (int di);
w_incr <= di;
endmethod
endmodule

### B.4.7 PulseWire

Interfaces and Methods

ThePulseWireinterface is anRWirewithout any data. It is useful within rules and action methods
to signal other methods or rules in the same clock cycle. Note that because the read method is called
_read, the register shorthand can be used to get its value without mentioning the method_read(it
is implicitly added).

```
PulseWireInterface
Name Type Description
send Action sends a signal down the wire
_read Bool returns the valid signal
```
```
interface PulseWire;
method Action send();
method Bool _read();
endinterface
```
Modules


#### B.4.9 WriteOnly Reference Guide Bluespec SystemVerilog

Four modules are provided to create aPulseWire, the only difference being the scheduling annota-
tions. In theORversions thesendmethod does not conflict with itself. Calling thesendmethod
for amkPulseWirefrom 2 rules causes the two rules to conflict while in themkPulseWireORthere is
no conflict. In other words, themkPulseWireORacts a logical ”OR”. TheUnsafeversions allow the
sendand_readmethods to be in the same rule.

```
mkPulseWire The writing to this type of wire is used in rules and action methods
to send a single bit to signal other methods or rules in the same
clock cycle.
```
```
module mkPulseWire(PulseWire);
```
```
mkPulseWireOR Returns aPulseWirewhich acts like a logical ”Or”. Thesend
method of the same wire can be used in two different rules without
conflict.
```
```
module mkPulseWireOR(PulseWire);
```
```
mkUnsafePulseWire The writing to this type of wire is used in rules and action methods
to send a single bit to signal other methods or rules in the same
clock cycle. Thesendand_readmethods can be in the same rule.
```
```
module mkUnsafePulseWire(PulseWire);
```
```
mkUnsafePulseWireOR Returns aPulseWirewhich acts like a logical ”Or”. Thesend
method of the same wire can be used in two different rules without
conflict. Thesendand_readmethods can be in the same rule.
```
```
module mkUnsafePulseWireOR(PulseWire);
```
```
Scheduling Annotations
mkPulseWire
read send
read CF SAR
send SBR C
```
```
Scheduling Annotations
mkUnsafePulseWire
read send
read CF SA
send SB C
```
```
Scheduling Annotations
mkPulseWireOR
read send
read CF SAR
send SBR SBR
```
```
Scheduling Annotations
mkUnsafePulseWireOR
read send
read CF SA
send SB SBR
```
Counter Example - UsingRegandPulseWire

interface Counter#(type size_t);
method Bit#(size_t) read();
method Action load(Bit#(size_t) newval);
method Action increment();


Reference Guide Bluespec SystemVerilog

method Action decrement();
endinterface

module mkCounter(Counter#(size_t));
Reg#(Bit#(size_t)) value <- mkReg(0); // define a Reg

```
PulseWire increment_called <- mkPulseWire(); // define the PulseWires used
PulseWire decrement_called <- mkPulseWire(); // to signal other methods or rules
```
```
// whether rules fire is based on values of PulseWires
rule do_increment(increment_called && !decrement_called);
value <= value + 1;
endrule
```
```
rule do_decrement(!increment_called && decrement_called);
value <= value - 1;
endrule
```
```
method Bit#(size_t) read(); // read the register
return value;
endmethod
```
```
method Action load(Bit#(size_t) newval); // load the register
value <= newval; // with a new value
endmethod
```
```
method Action increment(); // sends the signal on the
increment_called.send(); // PulseWire increment_called
endmethod
```
method Action decrement(); / sends the signal on the
decrement_called.send(); // PulseWire decrement_called
endmethod
endmodule

### B.4.8 ReadOnly

ReadOnlyis an interface which provides a value. The_readshorthand can be used to read the
value.

Interfaces and Methods

```
ReadOnlyInterface
Method
Name Type Description
_read a_type Reads the data
```
```
interface ReadOnly #( type a_type ) ;
method a_type _read() ;
endinterface
```
Functions


Bluespec SystemVerilog Reference Guide

```
regToReadOnly Converts aReginterface into aReadOnlyinterface. Useful for giving as the
argument to higher-order vector and list functions.
```
```
function ReadOnly#(a_type) regToReadOnly(Reg#(a_type) regIfc);
```
```
pulseWireToReadOnly Converts aPulseWireinterface into aReadOnlyinterface.
```
```
function ReadOnly#(Bool) pulseWireToReadOnly(PulseWire ifc);
```
```
readReadOnly Takes aReadOnlyinterface and returns a value.
```
```
function a_type readReadOnly(ReadOnly#(a_type) r);
```
Examples

```
interface AHBSlaveIFC;
interface AHBSlave bus;
interface Put#(AHBResponse) response;
interface ReadOnly#(AHBRequest) request;
endinterface
...
interface ReadOnly request;
method AHBRequest _read;
let ctrl = AhbCtrl {command: write_wire,
size: size_wire,
burst: burst_wire,
transfer: transfer_wire,
prot: prot_wire,
addr: addr_wire} ;
let value = AHBRequest {ctrl: ctrl, data: wdata_wire};
return value;
endmethod
endinterface
```
B.4.9 WriteOnly

WriteOnlyis an interface which writes a value. The_writeshorthand is used to write the value.

Interfaces and Methods

```
WriteOnlyInterface
Method Arguments
Name Type Description Name Description
_write Action Writes the data x Value to be written, of
datatypea_type.
```
interface WriteOnly #( type a_type ) ;
method Action _write (a_type x) ;
endinterface


Reference Guide Bluespec SystemVerilog

Examples

interface WriteOnly#(type a);
method Action _write(a v);
endinterface

// module with an always-enabled port to tie to a default value
import "BVI" AlwaysWrite =
module mkAlwaysWrite(WriteOnly#(a)) provisos(Bits#(a,sa));
no_reset;
parameter width = valueof(sa);
method _write(D_IN) enable((*inhigh*)EN);
schedule _write C _write;
endmodule

```
module mkDefaultValue1();
WriteOnly#(UInt#(7)) d1 <- mkAlwaysWrite(clocked_by primMakeDisabledClock);
rule handle_d1;
d1 <= 5;
endrule
endmodule
```